#    ________  __   ___    _______     __         _______   __     
#   /"       )|/"| /  ")  |   __ "\   /""\       |   __ "\ |" \    
#  (:   \___/ (: |/   /   (. |__) :) /    \      (. |__) :)||  |   
#   \___  \   |    __/    |:  ____/ /' /\  \     |:  ____/ |:  |   
#    __/  \\  (// _  \    (|  /    //  __'  \    (|  /     |.  |   
#   /" \   :) |: | \  \  /|__/ \  /   /  \\  \  /|__/ \    /\  |\  
#  (_______/  (__|  \__)(_______)(___/    \___)(_______)  (__\_|_)
# 
#      ğ’ğ¤ğ«ğ¢ğ©ğ­ğ€ğğˆ ğ°ğ¢ğ­ğ¡ ğ­ğ¨ğ§ğ¬ ğ¨ğŸ ğ®ğ¬ğğŸğ®ğ¥ ğŸğğšğ­ğ®ğ«ğğ¬, ğ¦ğ¨ğ¬ğ­ğ¥ğ² ğŸğ¨ğ« ğ©ğšğœğ¤ğğ­ ğ¬ğ­ğ®ğŸğŸ.
#
#   ______________________________________________________________
#
#  Requirements: Skript, skript-reflect, ThatPacketAddon, Reqn, SkriptJSON
#
#  Tested on Paper 1.16.5
#  Most of the things should work for servers with spigot 1.13+, but I didn't test it!
#  Some things can maybe even work on 1.8-1.12, but I won't give support for these versions of Minecraft,
#  so don't leave bad review just because it doesn't work for your 1.8 server. â™¥ Thanks for understanding!
#
#  If you run into problems, feel free to message me on discord: pesekjan#5182
#
#  DOCUMENTATION: https://app.gitbook.com/@jakub-zahomolkou/s/skpapi/
#  ORIGINAL PAGE OF SCRIPT: https://forums.skunity.com/resources/skpapi.1268/
#
#  ï¼£ï½’ï½…ï½„ï½‰ï½”ï½“ (ã£â—”â—¡â—”)ã£ â™¥
#    Big thanks to Mr.Darth for helping me with packets and reflection (https://forums.skunity.com/members/mr-darth.11173/)
#    Govindas for helping me with some packets for NPCs (https://forums.skunity.com/members/govindas.643/)

options:
	check-for-updates: true


#        âš  Do not change the code below or I will not âš 
#            âš  be able to help you with problems âš 
#   ______________________________________________________________

	version: 1.0-BETA.6

import:
	org.bukkit.Bukkit

option nms:
	get: 
		set {_nms} to Bukkit.getServer().getClass().getPackage().getName().split("\.")[3]
		return "net.minecraft.server.%{_nms}%"

import:
	java.util.Optional
	java.util.Arrays

	{@nms}.IRegistry
	{@nms}.MinecraftKey
	{@nms}.BlockPosition
	
	{@nms}.PacketPlayOutGameStateChange
	{@nms}.BossBattle$BarStyle as BarStyleNMS
	{@nms}.PacketPlayOutBoss$Action as BarActionsNMS
	{@nms}.PacketPlayOutCollect

	org.bukkit.ChatColor
	org.bukkit.entity.EntityType
	org.bukkit.boss.BarColor
	org.bukkit.boss.BarStyle
	
	ch.njol.skript.Skript

	com.comphenix.protocol.wrappers.WrappedDataWatcher$Registry
	com.comphenix.protocol.wrappers.WrappedDataWatcher$WrappedDataWatcherObject
	com.comphenix.protocol.wrappers.WrappedChatComponent
	com.comphenix.protocol.wrappers.Pair
	com.comphenix.protocol.wrappers.Vector3F
	com.comphenix.protocol.wrappers.BukkitConverters
	com.comphenix.protocol.wrappers.EnumWrappers
	com.comphenix.protocol.wrappers.EnumWrappers$EntityPose
	com.comphenix.protocol.wrappers.EnumWrappers$ItemSlot
	com.comphenix.protocol.wrappers.EnumWrappers$EntityUseAction
	com.comphenix.protocol.wrappers.EnumWrappers$WorldBorderAction
	com.comphenix.protocol.utility.MinecraftReflection


on load:
	set {wccConverter} to BukkitConverters.getWrappedChatComponentConverter()
	set {chatFormatEnum} to MinecraftReflection.getMinecraftClass("EnumChatFormat")
	set {postypes::*} to ...EntityPose.values()
	set {chatcolors::*} to ...ChatColor.values()
	set {barcolors::*} to ...BarColor.values()
	set {bartypes::*} to ...BarStyleNMS.values()
	set {baractions::*} to ...BarActionsNMS.values()


on load:
	set {_skript-reflect.ver} to "2.1.1"
	set {_ThatPacketAddon.ver} to "1.0-BETA.3"
	set {_Reqn.ver} to "1.2.1"
	set {_SkriptJSON.ver} to "1.0.0"
	loop "skript-reflect", "ThatPacketAddon", "SkriptJSON", "Reqn":
		if server.getServer().getPluginManager().getPlugin(loop-value) is not set:
			Skript.error("&c&lSKPAPI: &cYou missing addon: %loop-value%")
		else:
			(server.getServer().getPluginManager().getPlugin(loop-value).getDescription().getVersion()) is not {_%loop-value%.ver}
			Skript.warning("&6&lSKPAPI: &6Detected %loop-value% %(server.getServer().getPluginManager().getPlugin(loop-value).getDescription().getVersion())%, recommended %loop-value% version is %{_%loop-value%.ver}%")	
	if {@check-for-updates} is true:
		send a "get" request to "https://raw.githubusercontent.com/Pesekjak/skPapi/main/version.txt"
		set {_resp} to the last http response
		set {_LastVer} to {_resp}'s body
		set {_LastVerChars::*} to {_LastVer} split by ""
		set {_LastVer} to ""
		loop {_LastVerChars::*}:
			if loop-value is "." or "1" or "2" or "3" or "4" or "5" or "6" or "7" or "8" or "9" or "0" or "-" or "B" or "E" or "T" or "A":
				set {_LastVer} to "%{_LastVer}%%loop-value%"
		send a "get" request to "https://raw.githubusercontent.com/Pesekjak/skPapi/main/changelog.txt"
		set {_resp} to the last http response
		set {_ChangeLog} to {_resp}'s body
		if {_LastVer} is not "{@version}":
			Skript.warning("&6&lSKPAPI: &6Detected version {@version}&6, the newest version is %{_LastVer}%")
			loop all players where [input is op]:
				send "&6&lSKPAPI: &6Detected version {@version}&6, the newest version is %{_LastVer}%" to loop-player
				send "" to loop-player
				send "&6%{_ChangeLog}%" to loop-player
				send formatted "&6Gew the newest version here: &9&n<link:https://github.com/Pesekjak/skPapi>github.com/Pesekjak/skPapi<reset>" to loop-player


#   ______________________________________________________________


#                                    _                 
#                                   (_)                
#   _____  ___ __  _ __ ___  ___ ___ _  ___  _ __  ___ 
#  / _ \ \/ / '_ \| '__/ _ \/ __/ __| |/ _ \| '_ \/ __|
# |  __/>  <| |_) | | |  __/\__ \__ \ | (_) | | | \__ \
#  \___/_/\_\ .__/|_|  \___||___/___/_|\___/|_| |_|___/
#           | |                                        
#           |_|


# Name: Random UUID - generates random UUID
# Example: new classic uuid
expression new (1Â¦classic|2Â¦trimmed) uuid:
	return type: string
	get:
		set {_uuid} to random uuid
		if parse mark is 2:
			replace "-" with "" in {_uuid}
		return {_uuid}


# Name: JSON Object - Returns JSON object from text, useful for chat components
# Example: jsonobject from text "&c[ADMIN]"
expression jsonobject from text %string%:
	return type: jsonobject
	get:
		set {_json::text} to expr-1
		return json of list variable {_json::*}


# Name: Entity ID - Returns ID of certain entities, useful for packets
# Example: set {_id} to entity id of player's target
expression entity id of %entities%:
	return type: integers
	get:
		loop exprs-1:
			add (loop-expression.getEntityId()) to {_ids::*}
		return {_ids::*}


# Name: Entity type ID - Returns entity type ID, useful for summoning client side entity
# Example: set {_id} to entity type id of player's target
expression entity type id of %entities%:
	return type: integers
	get:
		loop exprs-1:
			set {_ent} to "%loop-expression%"
			set {_id} to IRegistry.ENTITY_TYPE.a(IRegistry.ENTITY_TYPE.get(new MinecraftKey("%{_ent}%")))
			set {_id} to {_id} parsed as integer
			add {_id} to {_ids::*}
		return {_ids::*}
		

#   ______________________________________________________________


#        __  __          _       
#       / _|/ _|        | |      
#   ___| |_| |_ ___  ___| |_ ___ 
#  / _ \  _|  _/ _ \/ __| __/ __|
# |  __/ | | ||  __/ (__| |_\__ \
#  \___|_| |_| \___|\___|\__|___/


# Name: Store UUID - Stores uuid from player's nickname
# Example: store uuid from nick "Notch" to {_uuid}
effect store uuid from nick %string% to %~objects%:
	trigger:
		delay the effect
		send a "get" request to "https://api.mojang.com/users/profiles/minecraft/%expr-1%"
		set {_resp} to the last http response
		set {_body} to {_resp}'s body
		copy json {_body} to {_output::*}
		set raw expr-2 to {_output::id}
		continue


# Name: Store skin - Stores skin from uuid
# Example: store skin from uuid {_uuid} to {_skin}
effect store skin from uuid %string% to %~objects%:
	trigger:
		delay the effect
		send a "get" request to "https://sessionserver.mojang.com/session/minecraft/profile/%expr-1%?unsigned=false"
		set {_resp} to the last http response
		set {_body} to {_resp}'s body
		copy json {_body} to {_output::*}
		set raw expr-2 to skin with value "%{_output::properties::1::value}%" signature "%{_output::properties::1::signature}%"
		continue


# Name: Destroy entity - Destroys entities from players.
# Example: destroy player's target from player:
effect destroy %entities% (from|for) %players%:
	trigger:
		set {_packet} to new play_server_entity_destroy packet
		loop exprs-1:
			set {_count} to ({_count} ? 1) + 1
			set {_id::%{_count}%} to loop-expression.getEntityId()
		set int array field of {_packet} to {_id::*}
		send exprs-2 packet {_packet}


# Name: Destroy entity with id - Destroys entity with certain ID from players. (Use this for client side entities)
# Example: destroy entity with id 101 from player:
effect destroy entity with id %number% from %players%:
	trigger:
		set {_packet} to new play_server_entity_destroy packet
		set int array field of {_packet} to expr-1
		send exprs-2 packet {_packet}


# Name: Client side entity - Spawns client side entity for players.
# Example: summon clientside entity 5 with id 101 with uuid (new classic uuid) at player's location for player
effect summon clientside entity %number% with id %number% with uuid %string% at %location% for %players%:
	trigger:
		set {_packet} to new play_server_spawn_entity_living packet
		set int field 1 of {_packet} to expr-1
		set int field 0 of {_packet} to expr-2
		set uuid field 0 of {_packet} to expr-3
		set double field 0 of {_packet} to x-pos of expr-4
		set double field 1 of {_packet} to y-pos of expr-4
		set double field 2 of {_packet} to z-pos of expr-4
		send exprs-5 packet {_packet}


# Name: Move client side entity - Moves with client side entity for players.
# Example: move clientside entity with id 101 by 1600 0 1600 for player
effect move clientside entity [with id] %number% by [x:] %number% [y:] %number% [z:] %number% for %players%:
	trigger:
		set {_packet} to new play_server_rel_entity_move packet
		set int pnum 0 of {_packet} to expr-1
		set short pnum 0 of {_packet} to expr-2
		set short pnum 1 of {_packet} to expr-3
		set short pnum 2 of {_packet} to expr-4
		send exprs-5 packet {_packet}


# Name: Rotate client side entity - Rotates with client side entity for players.
# Example: rotate clientside entity with id 101 by 16 0 for player
effect rotate clientside entity [with id] %number% by [yaw:] %number% [pitch:] %number% for %players%:
	trigger:
		set {_packet} to new play_server_entity_look packet
		set int pnum 0 of {_packet} to expr-1
		set float field 0 of {_packet} to (expr-2*256/360)
		set float field 1 of {_packet} to (expr-3*256/360)
		send exprs-4 packet {_packet}


# Name: Rotate head - Rotates head of entity with id for certain players.
# Example: rotate clientside entity with id 101 by 90 for player:
effect rotate clientside entity with id %number% by %number% for %players%:
	trigger:
		set {_packet} to new play_server_entity_head_rotation packet
		set int pnum 0 of {_packet} to expr-1
		set byte field 0 of {_packet} to (expr-2*256/360)
		send exprs-3 packet {_packet}


# Name: Teleport client side entity - Teleports client side entity for players.
# Example: teleport clientside entity with id 101 to player's location for player
effect teleport clientside entity [with id] %number% to %location% for %players%:
	trigger:
		set {_packet} to new play_server_entity_teleport packet
		set int pnum 0 of {_packet} to expr-1
		set double field 0 of {_packet} to (x-coord of expr-2)
		set double field 1 of {_packet} to (y-coord of expr-2)
		set double field 2 of {_packet} to (z-coord of expr-2)
		set float field 0 of {_packet} to (yaw of expr-2)
		set float field 1 of {_packet} to (pitch of expr-2)
		send exprs-3 packet {_packet}


# Name: Elytra animation - Applies elytra flying animation to client side entity for players.
# Example: force clientside entity with id 101 to fly for player:
effect force clientside entity [with id] %number% to (1Â¦fly|2Â¦stand) for %players%:
	trigger:
		set {_packet} to new play_server_entity_metadata packet
		set {_dw} to new datawatcher
		set watched byte 0 of {_dw} to (128.byteValue() if parse mark = 1, else 0.byteValue())
		set watchable collection field 0 of {_packet} to {_dw}
		set int field 0 of {_packet} to expr-1
		send exprs-2 packet {_packet}


# Name: Glow client side entity - Applies glow effect to client side entity for players.
# Example: make clientside entity with id 101 glow for player:
effect make clientside entity [with id] %number% (1Â¦glow|2Â¦unglow) for %players%:
	trigger:
		set {_packet} to new play_server_entity_metadata packet
		set {_dw} to new datawatcher
		set watched byte 0 of {_dw} to (64.byteValue() if parse mark = 1, else 0.byteValue())
		set watchable collection field 0 of {_packet} to {_dw}
		set int field 0 of {_packet} to expr-1
		send exprs-2 packet {_packet}


# Name: Make client side entity invisible - Applies invisible effect to client side entity for players.
# Example: make clientside entity with id 101 glow for player:
effect make clientside entity [with id] %number% (1Â¦invisible|2Â¦not invisible) for %players%:
	trigger:
		set {_packet} to new play_server_entity_metadata packet
		set {_dw} to new datawatcher
		set watched byte 0 of {_dw} to (32.byteValue() if parse mark = 1, else 0.byteValue())
		set watchable collection field 0 of {_packet} to {_dw}
		set int field 0 of {_packet} to expr-1
		send exprs-2 packet {_packet}


# Name: Fire client side entity - Applies fire effect to client side entity for players.
# Example: make clientside entity with id 101 burn for player:
effect make clientside entity [with id] %number% (1Â¦burn|2Â¦stop burn) for %players%:
	trigger:
		set {_packet} to new play_server_entity_metadata packet
		set {_dw} to new datawatcher
		set watched byte 0 of {_dw} to (1.byteValue() if parse mark = 1, else 0.byteValue())
		set watchable collection field 0 of {_packet} to {_dw}
		set int field 0 of {_packet} to expr-1
		send exprs-2 packet {_packet}


# Name: Change name of clientside entity - Changes name of client side entity
# Example: change clientside entity with id 101 name to "My entity" for player
effect change clientside entity with id %number% name to %string% for %players%:
	trigger:
		set {_metadata} to new play_server_entity_metadata packet
		set {_wcc} to WrappedChatComponent.fromText("%expr-2%")
		set {_nmsCC} to {wccConverter}.getGeneric({_wcc})
		set {_name} to Optional.of({_nmsCC})
		set {_dw} to new datawatcher
		set watched boolean 3 of {_dw} to true
		set {_serializer} to Registry.getChatComponentSerializer(true)
		{_dw}.setObject(2, {_serializer}, {_name})
		set watchable collection field 0 of {_metadata} to {_dw}
		set int field 0 of {_metadata} to expr-1
		send exprs-3 packet {_metadata}


# Name: Client side holo - Creates client side hologram, for removing use "Destroy entity with id" effect
# Example: create clientside holo "My Hologram" with id 101 at player's location for player
effect create clientside holo %string% with id %number% at %location% for %players%:
	trigger:
		set {_packet} to new play_server_spawn_entity_living packet
		set int field 1 of {_packet} to 1
		set int field 0 of {_packet} to expr-2
		set double field 0 of {_packet} to x-pos of expr-3
		set double field 1 of {_packet} to y-pos of expr-3
		set double field 2 of {_packet} to z-pos of expr-3
		send expr-4 packet {_packet}
		set {_metadata} to new play_server_entity_metadata packet
		set {_wcc} to WrappedChatComponent.fromText("%expr-1%")
		set {_nmsCC} to {wccConverter}.getGeneric({_wcc})
		set {_name} to Optional.of({_nmsCC})
		set {_dw} to new datawatcher
		set watched byte 0 of {_dw} to 32.byteValue()
		set watched byte 14 of {_dw} to 16.byteValue()
		set watched boolean 3 of {_dw} to true
		set {_serializer} to Registry.getChatComponentSerializer(true)
		{_dw}.setObject(2, {_serializer}, {_name})
		set watchable collection field 0 of {_metadata} to {_dw}
		set int field 0 of {_metadata} to expr-2
		send exprs-4 packet {_metadata}


# Name: Update side holo - Updates text of client side hologram
# Example: update clientside holo with id 101 with text "Updated text" for player
effect update clientside holo [with id] %number% with text %string% for %players%:
	trigger:
		set {_metadata} to new play_server_entity_metadata packet
		set {_wcc} to WrappedChatComponent.fromText("%expr-2%")
		set {_nmsCC} to {wccConverter}.getGeneric({_wcc})
		set {_name} to Optional.of({_nmsCC})
		set {_dw} to new datawatcher
		set watched byte 0 of {_dw} to 32.byteValue()
		set watched boolean 3 of {_dw} to true
		set {_serializer} to Registry.getChatComponentSerializer(true)
		{_dw}.setObject(2, {_serializer}, {_name})
		set watchable collection field 0 of {_metadata} to {_dw}
		set int field 0 of {_metadata} to expr-1
		send exprs-3 packet {_metadata}


# Name: Change skin - Changes player's skin for certain players, for visibility call this effect after switching dimensions
# Example: change skin of player to {_skin} for all players
effect change skin of %player% to %skin% for %players%:
	trigger:
		set {_packet} to new play_server_player_info packet
		set "PlayerInfoAction" penum 0 of {_packet} to "ADD_PLAYER"
		set {_a::gamemode} to creative
		set {_a::gameprofile::name} to expr-1's nickname
		set {_a::gameprofile::skin} to expr-2
		set {_a::latency} to 0
		set {_a::gameprofile::uuid} to expr-1's uuid
		set "PlayerInfoData" array pjson 0 of {_packet} to json of listvar {_a::*}
		send exprs-3 packet {_packet}


# Name: Change nick - Changes player's nick for certain players, for visibility call this effect after switching dimensions
# Example: change nick of player to "deadmau5" with skin {_skin} for all players
effect change nick of %player% to %string% with skin %skin% for %players%:
	trigger:
		set {_packet} to new play_server_player_info packet
		set "PlayerInfoAction" penum 0 of {_packet} to "ADD_PLAYER"
		set {_a::gamemode} to creative
		set {_a::gameprofile::name} to expr-2
		set {_a::gameprofile::skin} to expr-3
		set {_a::latency} to 0
		set {_a::gameprofile::uuid} to expr-1's uuid
		set "PlayerInfoData" array pjson 0 of {_packet} to json of listvar {_a::*}
		send exprs-4 packet {_packet}


# Name: Remove player from tablist - Removes player from tablist of certain players
# Example: remove player from tablist of all players
effect remove %player% from tablist of %players%:
	trigger:
		set {_packet} to new play_server_player_info packet
		set "PlayerInfoAction" penum 0 of {_packet} to "REMOVE_PLAYER"
		set {_a::gamemode} to creative
		set {_a::gameprofile::name} to expr-1
		store uuid from nick expr-1's nickname to {_uuid}
		store skin from uuid {_uuid} to {_skin}
		set {_a::gameprofile::skin} to {_skin}
		set {_a::latency} to 0
		set {_a::gameprofile::uuid} to expr-1's uuid
		set "PlayerInfoData" array pjson 0 of {_packet} to json of listvar {_a::*}
		send exprs-2 packet {_packet}


# Name: Add player to tablist - Adds player to tablist of certain players
# Example: add player to tablist of all players
effect add %player% to tablist of %players%:
	trigger:
		set {_packet} to new play_server_player_info packet
		set "PlayerInfoAction" penum 0 of {_packet} to "ADD_PLAYER"
		set {_a::gamemode} to creative
		set {_a::gameprofile::name} to expr-1
		store uuid from nick expr-1's nickname to {_uuid}
		store skin from uuid {_uuid} to {_skin}
		set {_a::gameprofile::skin} to {_skin}
		set {_a::latency} to 0
		set {_a::gameprofile::uuid} to expr-1's uuid
		set "PlayerInfoData" array pjson 0 of {_packet} to json of listvar {_a::*}
		send exprs-2 packet {_packet}


# Name: Show demo screen - Shows demo screen to players
# Example: show demoscreen to player
effect show demo[ ]screen to %players%:
	trigger:
		set {_packet} to new PacketPlayOutGameStateChange(PacketPlayOutGameStateChange.f, 0)
		loop exprs-1:
			loop-value.getHandle().playerConnection.sendPacket({_packet})


# Name: Start rain - Starts client side rain for players
# Example: force rain start for player
effect force rain (1Â¦start|2Â¦stop) for %players%:
	trigger:
		set {_packet} to ((new PacketPlayOutGameStateChange(PacketPlayOutGameStateChange.c, 0)) if parse mark = 1, else (new PacketPlayOutGameStateChange(PacketPlayOutGameStateChange.b, 0)))
		loop exprs-1:
			loop-value.getHandle().playerConnection.sendPacket({_packet})


# Name: Custom sky - Change client side color of the sky for players. keep in mind that rain has to be on (I don't recommend going over 21, use -1 for brighter stars)
# Example: change sky color to 5 for player
effect change sky color to %number% for %players%:
	trigger:
		set {_packet} to new PacketPlayOutGameStateChange(PacketPlayOutGameStateChange.h, expr-1)
		player.getHandle().playerConnection.sendPacket({_packet})
		loop exprs-2:
			loop-value.getHandle().playerConnection.sendPacket({_packet})


# Name: Player tag visibility - Changes player's tag visibility to certain players
# Example: set visibility to tag "Notch" to false with teamname "hiddentag_%player%" for %players%
effect set visibility to tag %string% to (1Â¦true|2Â¦false) with teamname %string% for %players%:
	trigger:
		set {_packet} to new play_server_scoreboard_team packet
		set byte field 0 of {_packet} to 0 
		set string field 0 of {_packet} to expr-2
		{_packet}.getEnumModifier(ChatColor, {chatFormatEnum}).write(0, ChatColor.WHITE)
		if parse mark is 1:
			set string field 1 of {_packet} to "always" 
		else:
			set string field 1 of {_packet} to "never" 
		set collection field 0 of {_packet} to expr-1
		send exprs-3 packet {_packet}


# Name: Player tags - Changes player's tags (prefix/suffix) to certain players (for entities use their UUIDs, for players use their nicknames)
# Example: change player's nickname tags to "&c[ADMIN] " and " &6VIP" with color "&c" with teamname "1A" to all players
effect change %string% tags to %string% and %string% with color %string% with teamname %string% to %players%:
	trigger:
		set {_packet} to new play_server_scoreboard_team packet
		set byte field 0 of {_packet} to 0 
		set string field 0 of {_packet} to expr-5
		set {_colorsymbols::*} to "&0", "&1", "&2", "&3", "&4", "&5", "&6", "&7", "&8", "&9", "&a", "&b", "&c", "&d", "&e", "&f", "&k", "&l", "&m", "&n", "&o", "&r"
		loop {_colorsymbols::*}:
			expr-4 = loop-value
			set {_num} to loop-index
			stop loop
		{_packet}.getEnumModifier(ChatColor, {chatFormatEnum}).write(0, {chatcolors::%{_num}%})
		set chat component field 1 of {_packet} to (jsonobject from text expr-2)
		set chat component field 2 of {_packet} to (jsonobject from text expr-3)
		set collection field 0 of {_packet} to expr-1
		send exprs-6 packet {_packet}


# Name: Fake tablist player - Adds fake player to tablist of certain players, you can use tab in chat
# Example: add fake tablist player "Notch" with skin {_skin} with uuid (new classic uuid) for all players
effect add fake [tablist] player %string% [with] skin %skin% [with] uuid %string% for %players%:
	trigger:
		set {_packet} to new play_server_player_info packet
		set "PlayerInfoAction" penum 0 of {_packet} to "ADD_PLAYER"
		set {_a::gamemode} to creative
		set {_a::gameprofile::name} to expr-1
		set {_a::gameprofile::skin} to expr-2
		set {_a::latency} to 0
		set {_a::gameprofile::uuid} to expr-3
		set "PlayerInfoData" array pjson 0 of {_packet} to json of listvar {_a::*}
		send exprs-4 packet {_packet}

# Name: Remove fake tablist player - Removes fake player to tablist of certain players, you can use tab in chat
# Example: remove fake tablist player with uuid (new classic uuid) for all players
effect remove fake [tablist] player with uuid %string% for %players%:
	trigger:
		set {_packet} to new play_server_player_info packet
		set "PlayerInfoAction" penum 0 of {_packet} to "REMOVE_PLAYER"
		set {_a::gamemode} to creative
		set {_a::gameprofile::name} to "Steve"
		set {_a::gameprofile::skin} to steve
		set {_a::latency} to 0
		set {_a::gameprofile::uuid} to expr-1
		set "PlayerInfoData" array pjson 0 of {_packet} to json of listvar {_a::*}
		send exprs-2 packet {_packet}


# Name: Guardian - Plays guardian effect for certain players
# Example: play guardian effect with sounds for player
effect play guardian effect (1Â¦with|2Â¦without) sounds for %players%:
	trigger:
		if parse mark is 1:
			set {_packet} to new PacketPlayOutGameStateChange(PacketPlayOutGameStateChange.k, 1)
		else:
			set {_packet} to new PacketPlayOutGameStateChange(PacketPlayOutGameStateChange.k, 0)
		player.getHandle().playerConnection.sendPacket({_packet})
		loop exprs-1:
			loop-value.getHandle().playerConnection.sendPacket({_packet})


# Name: Totem - Plays totem effect for certain players
# Example: play totem effect for player
effect play totem effect for %players%:
	trigger:
		set {_packet} to new play_server_entity_status packet
		loop exprs-1:
			set int field 0 of {_packet} to loop-expression.getEntityId()
			set byte field 0 of {_packet} to 35
			send loop-expression packet {_packet}


# Name: Finish action - Finishes action for player (client side)
# Example: finish action for player
effect finish action for %players%:
	trigger:
		set {_packet} to new play_server_entity_status packet
		loop exprs-1:
			set int field 0 of {_packet} to loop-expression.getEntityId()
			set byte field 0 of {_packet} to 9
			send loop-expression packet {_packet}


# Name: Position changer - Changes position of player, only other player's can see it
# Example: set position of player to "STANDING" for all players
effect set position of %player% to %string% for %players%:
	trigger:
		set {_packet} to new play_server_entity_metadata packet
		set int field 0 of {_packet} to (entity id of expr-1)
		set {_dw} to new datawatcher
		set {_sz} to Registry.get(EnumWrappers.getEntityPoseClass())
		set {_pos::*} to "STANDING", "FALL_FLYING", "SLEEPING", "SWIMMING", "SPIN_ATTACK", "CROUCHING", "DYING"
		loop {_pos::*}:
			expr-2 = loop-value
			set {_num} to loop-index
			stop loop
		{_dw}.setObject(6, {_sz}, {postypes::%{_num}%}.toNms())
		set watchable collection field 0 of {_packet} to {_dw}
		send exprs-3 packet {_packet}


# Name: Client gamemode - Change gamemode client sided
# Example: set clientgamemode of player to "survival"
effect set clientgamemode of %players% to %string%:
	trigger:
		if exprs-2 is "survival":
			set {_packet} to new PacketPlayOutGameStateChange(PacketPlayOutGameStateChange.d, 0)
		else if exprs-2 is "creative":
			set {_packet} to new PacketPlayOutGameStateChange(PacketPlayOutGameStateChange.d, 1)
		else if exprs-2 is "adventure":
			set {_packet} to new PacketPlayOutGameStateChange(PacketPlayOutGameStateChange.d, 2)
		else:
			set {_packet} to new PacketPlayOutGameStateChange(PacketPlayOutGameStateChange.d, 3)
		loop exprs-1:
			loop-value.getHandle().playerConnection.sendPacket({_packet})


# Name: Open chest - Opens/Closes chest for certain players
# Example: force chest at location of event-block to open for player
effect force chest at %location% to (1Â¦open|2Â¦close) for %players%:
	trigger:
		set {_packet} to new play_server_block_action packet
		set location field of {_packet} to expr-1
		set block field of {_packet} to block at expr-1
		set int field 0 of {_packet} to 1
		if parse mark is 1:
			set int field 1 of {_packet} to 1
		else:
			set int field 1 of {_packet} to 0
		send exprs-2 packet {_packet}


# Name: Clientside NPC - Creates clientside npc
# Example: create clientside npc with id 101 with name "NPC" with skin {_skin} with uuid (new classic uuid) with 2nd skin layer true at location player's location for player
effect create clientside npc with id %number% with name %string% with skin %skin% with uuid %string% with 2nd skin layer %boolean% at location %location% for %players%:
	trigger:
		set {_npcspawn} to new play_server_named_entity_spawn packet
		set int pnum 0 of {_npcspawn} to expr-1
		
		set {_packet} to new play_server_player_info packet
		set "PlayerInfoAction" penum 0 of {_packet} to "ADD_PLAYER"
		set {_a::gamemode} to creative
		set {_a::gameprofile::name} to expr-2
		set {_a::gameprofile::skin} to expr-3
		set {_a::latency} to 0
		set {_uuid} to expr-4
		set {_a::gameprofile::uuid} to {_uuid}
		set "PlayerInfoData" array pjson 0 of {_packet} to json of listvar {_a::*}
		send exprs-7 packet {_packet}
		set "uuid" pinfo 0 of {_npcspawn} to {_uuid}
		set double pnum 0 of {_npcspawn} to expr-6's x-loc
		set double pnum 1 of {_npcspawn} to expr-6's y-loc
		set double pnum 2 of {_npcspawn} to expr-6's z-loc
		set short pnum 0 of {_npcspawn} to 1
		set byte pnum 0 of {_npcspawn} to (expr-6's yaw)*256/360
		set byte pnum 1 of {_npcspawn} to (expr-6's pitch)*256/360
		send exprs-7 packet {_npcspawn}
		if expr-5 is true:
			set {_skinpacket} to new play_server_entity_metadata packet
			set {_dw} to new datawatcher
			set watched byte 16 of {_dw} to 255.byteValue()
			set int field 0 of {_skinpacket} to expr-1
			set watchable collection field 0 of {_skinpacket} to {_dw}
			send exprs-7 packet {_skinpacket}


# Name: Position changer from id - Changes position of entity with id for certain players
# Example: set position of entity with id 101 to "STANDING" for all players
effect set position of entity with id %number% to %string% for %players%:
	trigger:
		set {_packet} to new play_server_entity_metadata packet
		set int field 0 of {_packet} to expr-1
		set {_dw} to new datawatcher
		set {_sz} to Registry.get(EnumWrappers.getEntityPoseClass())
		set {_pos::*} to "STANDING", "FALL_FLYING", "SLEEPING", "SWIMMING", "SPIN_ATTACK", "CROUCHING", "DYING"
		loop {_pos::*}:
			expr-2 = loop-value
			set {_num} to loop-index
			stop loop
		{_dw}.setObject(6, {_sz}, {postypes::%{_num}%}.toNms())
		set watchable collection field 0 of {_packet} to {_dw}
		send exprs-3 packet {_packet}


# Name: Play entity animation - Play animation of entity with id for certain players
# Example: force entity with id 101 to make swing hand animation for player
effect force entity with id %number% to make (1Â¦swing hand|2Â¦damage|3Â¦leave bed|4Â¦swing offhand|5Â¦critical|6Â¦magic) animation for %players%:
	trigger:
		set {_packet} to new play_server_animation packet
		set int pnum 0 of {_packet} to expr-1
		set {_num} to parse mark - 1
		set object field 1 of {_packet} to {_num}.byteValue()
		send exprs-2 packet {_packet}


# Name: Cooldown - Sets cooldown of item
# Example: set cooldown for player of ender pearl to 20 ticks
effect set cooldown for %players% of %itemtype% to %number% ticks:
	trigger:
		loop expressions-1:
			loop-expression.setCooldown((random item of expr-2).getType(), expr-3)


# Name: Equip clientside - Equips entity clientside for certain players
# Example: eqiup clientside entity with id 101 with diamond sword air air air air air for player
effect eqiup clientside entity with id %number% with [hand] %itemtype% [offhand] %itemtype% [helmet] %itemtype% [chestplate] %itemtype% [leggings] %itemtype% [boots] %itemtype% for %players%:
	trigger:
		set {_packet} to new play_server_entity_equipment packet		
		set {_handslot} to new Pair(ItemSlot.MAINHAND, random item out of (expr-2))
		set {_offhandslot} to new Pair(ItemSlot.OFFHAND, random item out of (expr-3))
		set {_helmetslot} to new Pair(ItemSlot.HEAD, random item out of (expr-4))
		set {_chestslot} to new Pair(ItemSlot.CHEST, random item out of (expr-5))
		set {_leggslot} to new Pair(ItemSlot.LEGS, random item out of (expr-6))
		set {_bootsslot} to new Pair(ItemSlot.FEET, random item out of (expr-7))
		set {_list} to Arrays.asList({_handslot}, {_offhandslot}, {_helmetslot}, {_chestslot}, {_leggslot}, {_bootsslot})
		{_packet}.getSlotStackPairLists().write(0, {_list})
		set int field 0 of {_packet} to expr-1
		send exprs-8 packet {_packet}


# Name: Rotate AS client side  - Rotates client side armor stand for players.
# Example: rotate part head of clientside armorstand with id 101 by 15 15 15 for player
effect rotate part (1Â¦head|2Â¦body|3Â¦left arm|4Â¦right arm|5Â¦left leg|6Â¦right leg) of clientside armorstand with id %number% by %number% %number% %number% for %players%:
	trigger:
		set {_packet} to new play_server_entity_metadata packet
		set {_dw} to new datawatcher
		set {_num} to (parse mark + 14)
		set {_Serializer} to Registry.get(Vector3F.getMinecraftClass())
		set {_headRotation} to new WrappedDataWatcherObject({_num}, {_Serializer})
		set {_vecc} to new Vector3F(expr-2 and expr-3 and expr-4)
		{_dw}.setObject({_headRotation}, {_vecc})
		set watchable collection field 0 of {_packet} to {_dw}
		set int field 0 of {_packet} to expr-1
		send exprs-5 packet {_packet}


# Name: Armor stand types - Change types of armor stand for players. (1:Small, 4:Arms, 8:NoBase, 16:Marker - Count types together)
# Example: change clientside armorstand with id 101 type to 5 for player:
effect change clientside armorstand with id %number% type to %number% for %players%:
	trigger:
		set {_packet} to new play_server_entity_metadata packet
		set {_dw} to new datawatcher
		set watched byte 14 of {_dw} to expr-2.byteValue()
		set watchable collection field 0 of {_packet} to {_dw}
		set int field 0 of {_packet} to expr-1
		send exprs-3 packet {_packet}


# Name: Create teams - Creates teams for certain players
# Example: create clientside team with teamname "admins" with tags 0 with tag visibility "always" with collisions "never" with color "&c" with prefix "&c[&eADMIN&c] " with suffix "" for all players
effect create clientside team with teamname %string% with tags %number% with tag visibility %string% with collisions %string% with color %string% with prefix %string% with suffix %string% for %players%:
	trigger:
		set {_packet} to new play_server_scoreboard_team packet
		set int field 0 of {_packet} to 0
		set string field 0 of {_packet} to expr-1
		set byte field 1 of {_packet} to expr-2.byteValue()
		set string field 1 of {_packet} to expr-3
		set string field 2 of {_packet} to expr-4
		set {_colorsymbols::*} to "&0", "&1", "&2", "&3", "&4", "&5", "&6", "&7", "&8", "&9", "&a", "&b", "&c", "&d", "&e", "&f", "&k", "&l", "&m", "&n", "&o", "&r"
		loop {_colorsymbols::*}:
			expr-5 = loop-value
			set {_num} to loop-index
			stop loop
		{_packet}.getEnumModifier(ChatColor, {chatFormatEnum}).write(0, {chatcolors::%{_num}%})
		set chat component field 1 of {_packet} to (jsonobject from text expr-6)
		set chat component field 2 of {_packet} to (jsonobject from text expr-7)
		send exprs-8 packet {_packet}


# Name: Update teams - Updates teams for certain players
# Example: update clientside team with teamname "admins" with tags 0 with tag visibility "always" with collisions "never" with color "&c" with prefix "&c[&eADMIN&c] " with suffix "" for all players
effect update clientside team with teamname %string% with tags %number% with tag visibility %string% with collisions %string% with color %string% with prefix %string% with suffix %string% for %players%:
	trigger:
		set {_packet} to new play_server_scoreboard_team packet
		set int field 0 of {_packet} to 2
		set string field 0 of {_packet} to expr-1
		set byte field 1 of {_packet} to expr-2.byteValue()
		set string field 1 of {_packet} to expr-3
		set string field 2 of {_packet} to expr-4
		set {_colorsymbols::*} to "&0", "&1", "&2", "&3", "&4", "&5", "&6", "&7", "&8", "&9", "&a", "&b", "&c", "&d", "&e", "&f", "&k", "&l", "&m", "&n", "&o", "&r"
		loop {_colorsymbols::*}:
			expr-5 = loop-value
			set {_num} to loop-index
			stop loop
		{_packet}.getEnumModifier(ChatColor, {chatFormatEnum}).write(0, {chatcolors::%{_num}%})
		set chat component field 1 of {_packet} to (jsonobject from text expr-6)
		set chat component field 2 of {_packet} to (jsonobject from text expr-7)
		send exprs-8 packet {_packet}


# Name: Add players to team - Adds players to team, for players use their nicknames, for entites use UUIDs
# Example: add to clientside team with teamname "admins" entity player's nickname for all players
effect add to clientside team with teamname %string% entity %strings% for %players%:
	trigger:
		set {_packet} to new play_server_scoreboard_team packet
		set int field 0 of {_packet} to 3
		set string field 0 of {_packet} to expr-1
		set collection field 0 of {_packet} to exprs-2
		send exprs-3 packet {_packet}


# Name: Remove players to team - Removes players from team, for players use their nicknames, for entites use UUIDs
# Example: remove from clientside team with teamname "admins" entity player's nickname for all players
effect remove from clientside team with teamname %string% entity %string% for %players%:
	trigger:
		set string field 0 of {_packet} to expr-1
		set {_packet} to new play_server_scoreboard_team packet
		set int field 0 of {_packet} to 4
		set collection field 0 of {_packet} to expr-2
		send exprs-3 packet {_packet}


# Name: Remove team - Removes team with ID
# Example: remove clientside team with teamname "admins" for all players
effect remove clientside team with teamname %string% for %players%:
	trigger:
		set {_packet} to new play_server_scoreboard_team packet
		set int field 0 of {_packet} to 1
		set string field 0 of {_packet} to expr-1
		send exprs-2 packet {_packet}


# Name: Create boss bar - Creates boss bars for certain players
# Example: create clientside bossbar with uuid {_uuid} title "&b&lCLIENT SIDE BOSSBAR" health 0.5 color RED division 4 darksky true music true fog true for player
effect create clientside bossbar with uuid %string% title %string% health %number% color (1Â¦PINK|2Â¦BLUE|3Â¦RED|4Â¦GREEN|5Â¦YELLOW|6Â¦PURPLE|7Â¦WHITE) division %number% darksky %boolean% music %boolean% fog %boolean% for %players%:
	trigger:
		set {_packet} to new play_server_boss packet
		set uuid field 0 of {_packet} to expr-1
		set int field 0 of {_packet} to 0
		set chat component field 0 of {_packet} to (jsonobject from text expr-2)
		set float field 0 of {_packet} to expr-3
		{_packet}.getEnumModifier(BarColor, 4).write(0, {barcolors::%parse mark%})
		{_packet}.getEnumModifier(BarStyle, 5).write(0, {bartypes::%expr-4%})
		{_packet}.getBooleans().write(0, expr-5)
		{_packet}.getBooleans().write(1, expr-6)
		{_packet}.getBooleans().write(2, expr-7)
		send exprs-8 packet {_packet}


# Name: Remove boss bar - Removes boss bars for certain players
# Example: remove clientside bossbar with uuid {_uuid} for player
effect remove clientside bossbar with uuid %string% for %players%:
	trigger:
		set {_packet} to new play_server_boss packet
		set uuid field 0 of {_packet} to expr-1
		set object field 1 of {_packet} to {baractions::2}
		send exprs-2 packet {_packet}


# Name: Update health of boss bar - Updates boss bar's health for certain players
# Example: update health of clientside bossbar with uuid {_uuid} to 0 for player
effect update health of clientside bossbar with uuid %string% to %number% for %players%:
	trigger:
		set {_packet} to new play_server_boss packet
		set uuid field 0 of {_packet} to expr-1
		set object field 1 of {_packet} to {baractions::3}
		set float field 0 of {_packet} to expr-2
		send exprs-3 packet {_packet}


# Name: Update title of boss bar - Updates boss bar's title for certain players
# Example: update title of clientside bossbar with uuid {_uuid} to "New title" for player
effect update title of clientside bossbar with uuid %string% to %string% for %players%:
	trigger:
		set {_packet} to new play_server_boss packet
		set uuid field 0 of {_packet} to expr-1
		set object field 1 of {_packet} to {baractions::4}
		set chat component field 0 of {_packet} to (jsonobject from text expr-2)
		send exprs-3 packet {_packet}


# Name: Update style of boss bar - Updates boss bar's style for certain players
# Example: update style of clientside bossbar with uuid {_uuid} to color GREEN division 1 for player
effect update style of clientside bossbar with uuid %string% to color (1Â¦PINK|2Â¦BLUE|3Â¦RED|4Â¦GREEN|5Â¦YELLOW|6Â¦PURPLE|7Â¦WHITE) division %number% for %players%:
	trigger:
		set {_packet} to new play_server_boss packet
		set uuid field 0 of {_packet} to expr-1
		set object field 1 of {_packet} to {baractions::5}
		{_packet}.getEnumModifier(BarColor, 4).write(0, {barcolors::%parse mark%})
		{_packet}.getEnumModifier(BarStyle, 5).write(0, {bartypes::%expr-2%})
		send exprs-3 packet {_packet}


# Name: Update flags of boss bar - Updates boss bar's flags for certain players
# Example: update flags of clientside bossbar with uuid {_uuid} to darksky true music true fog false for %players%
effect update flags of clientside bossbar with uuid %string% to darksky %boolean% music %boolean% fog %boolean% for %players%:
	trigger:
		set {_packet} to new play_server_boss packet
		set uuid field 0 of {_packet} to expr-1
		set object field 1 of {_packet} to {baractions::6}
		{_packet}.getBooleans().write(0, expr-2)
		{_packet}.getBooleans().write(1, expr-3)
		{_packet}.getBooleans().write(2, expr-4)
		send exprs-5 packet {_packet}


# Name: Crash game - Crashes game for certain players
# Example: crash game for player
effect crash game for %players%:
	trigger:
		set {_uuid} to (new classic uuid)
		set {_packet} to new play_server_boss packet
		set uuid field 0 of {_packet} to {_uuid}
		set int field 0 of {_packet} to 2
		set float field 0 of {_packet} to 1
		send exprs-1 packet {_packet}


# Name: Header and footer - Changes header and footer for certain players
# Example: set header to "Test%nl%&6header" and footer to "&eI like SkPapi" for player
effect set header to %string% and footer to %string% for %players%:
	trigger:
		set {_packet} to new play_server_player_list_header_footer packet
		set chat component field 0 of {_packet} to (jsonobject from text expr-1)
		set chat component field 1 of {_packet} to (jsonobject from text expr-2)
		send exprs-3 packet {_packet}


# Name: Block break animation - Set block break animations of blocks for certain players
# Example: set block break of block at {_loc} to state 5 entity id (random integer between 0 and 9999) for player
effect set block break of block at %location% to [state] %number% entity id %number% for %players%:
	trigger:
		set {_packet} to new play_server_block_break_animation packet
		set location field 0 of {_packet} to expr-1
		set int field 0 of {_packet} to expr-3
		set int field 1 of {_packet} to expr-2
		loop exprs-4:
			send loop-expression packet {_packet}


# Name: Send resource pack - Sends resource pack request. For hash use lowercase SHA-1 hashed URL of pack
# Example: request pack from url "https://www.google.cz/" with hash "197cfd2e6827bee3c84bb2c7bcebbec2e7eaa0ea" for player
effect request pack from url %string% with hash %string% for %players%:
	trigger:
		set {_packet} to new play_server_resource_pack_send packet
		set string field 0 of {_packet} to expr-1
		set string field 1 of {_packet} to expr-2
		send exprs-3 packet {_packet}


# Name: Open sign gui - Opens client-side sign gui for player
# Example: open new signgui with text "", "===============", "TYPE NEW NICKNAME", "===============" type birch sign to player
effect open [new] sign[ ]gui with text %strings% type %itemtype% to %players%:
	trigger:
		set {_packet} to new play_server_open_sign_editor packet
		loop 4 times:
			set {_line::%loop-number%} to ""
		set {_count} to 1
		loop exprs-1:
			set {_line::%{_count}%} to loop-expression
			add 1 to {_count}
		set {_linesarray} to ["%{_line::1}%", "%{_line::2}%", "%{_line::3}%", "%{_line::4}%"]
		loop exprs-3:
			set {_players::%loop-expression%} to loop-expression
		loop {_players::*}:
			set {_loc::%loop-value%} to location behind loop-value
			make loop-value see block at {_loc::%loop-value%} as expr-2
		wait a tick
		loop {_players::*}:
			loop-value.sendSignChange({_loc::%loop-value%}, {_linesarray})
		wait a tick
		loop {_players::*}:
			set location field 0 of {_packet} to {_loc::%loop-value%}
			send packet {_packet} to loop-value


# Name: Set world border size - Sets size of world border
# Example: set size of client side world border to 10 for player
effect set size of client side world border to %number% for %players%:
	trigger:
		set {_packet} to new play_server_world_border packet
		{_packet}.getWorldBorderActions().write(0, WorldBorderAction.SET_SIZE)
		{_packet}.getDoubles().write(2, expr-1)
		{_packet}.getDoubles().write(3, expr-1)
		send exprs-2 packet {_packet}


# Name: Change world border center - Changes center of world border
# Example: set center of client side world border to X 800 Z -900 for player
effect set center of client side world border to X %number% Z %number% for %players%:
	trigger:
		set {_packet} to new play_server_world_border packet
		{_packet}.getWorldBorderActions().write(0, WorldBorderAction.SET_CENTER)
		{_packet}.getDoubles().write(0, expr-1)
		{_packet}.getDoubles().write(1, expr-2)
		send exprs-3 packet {_packet}


# Name: Warning size - Changes warning size of client-side world border (tints screen red)
# Example: set size of client side world border to 50 for player
effect set warning of client side world border to %number% for %players%:
	trigger:
		set {_packet} to new play_server_world_border packet
		{_packet}.getWorldBorderActions().write(0, WorldBorderAction.SET_WARNING_BLOCKS)
		set int field 2 of {_packet} to expr-1
		send exprs-2 packet {_packet}


# Name: Lerp size - Lerps size of world border (use milliseconds as speed)
# Example: lerp size of client side world border from 20 to 50 with speed 1000 for player
effect lerp size of client side world border from %number% to %number% with speed %number% for %players%:
	trigger:
		set {_packet} to new play_server_world_border packet
		{_packet}.getWorldBorderActions().write(0, WorldBorderAction.LERP_SIZE)
		set double field 3 of {_packet} to expr-1.doubleValue()
		set double field 2 of {_packet} to expr-2.doubleValue()
		set long field 0 of {_packet} to expr-3.longValue()
		send exprs-4 packet {_packet}


# Name: Initalize client-side world border - Initalizes client-side world border (use milliseconds as speed)
# Example: initalize client side world border to X (x-coord of player's location) Z (z-coord of player's location) from 50 to 20 with speed 5000 warning time 10 warning blocks 5 for player
effect initalize client side world border to X %number% Z %number% from %number% to %number% with speed %number% warning time %number% warning blocks %number% for %players%:
	trigger:
		set {_packet} to new play_server_world_border packet
		{_packet}.getWorldBorderActions().write(0, WorldBorderAction.INITIALIZE)
		set double field 0 of {_packet} to expr-1.doubleValue()
		set double field 1 of {_packet} to expr-2.doubleValue()
		set double field 3 of {_packet} to expr-3.doubleValue()
		set double field 2 of {_packet} to expr-4.doubleValue()
		set long field 0 of {_packet} to expr-5.longValue()
		set int field 0 of {_packet} to 29900000
		set int field 1 of {_packet} to expr-6
		set int field 2 of {_packet} to expr-7
		send exprs-8 packet {_packet}


# Name: Warning time - Changes warning time of client-side world border
# Example: set warning time of client side world border to 50 for player
effect set warning time of client side world border to %number% for %players%:
	trigger:
		set {_packet} to new play_server_world_border packet
		{_packet}.getWorldBorderActions().write(0, WorldBorderAction.SET_WARNING_TIME)
		set int field 1 of {_packet} to expr-1
		send exprs-2 packet {_packet}


# Name: Clientside time - Changes time for certain players
# Example: set clientside time to world age 0 time 16000 for player
effect set clientside time to world age %number% time %number% for %players%:
	trigger:
		set {_packet} to new play_server_update_time packet
		set long field 0 of {_packet} to expr-1
		set long field 1 of {_packet} to expr-2
		send exprs-3 packet {_packet}


# Name: Sleep - Makes player sleep
# Example: make player sleep at player's location for player
effect make %players% sleep at %location% for %players%:
	trigger:
		set {_packet} to new play_server_entity_metadata packet
		set {_OptionalBlockPos} to Optional.of(new BlockPosition(x location of expression-2, (y location of expression-2) - 0.5 and z location of expression-2))
		set {_dw} to new datawatcher
		set {_Serializer} to Registry.getBlockPositionSerializer(true)
		set {_DWO} to new WrappedDataWatcherObject(13 and {_Serializer})
		{_dw}.setObject({_DWO} and {_OptionalBlockPos})
		set {_Serializer2} to Registry.get(EnumWrappers.getEntityPoseClass())
		{_dw}.setObject(6, {_Serializer2}, EntityPose.SLEEPING.toNms())
		set watchable collection field 0 of {_packet} to {_dw}
		loop expressions-1:
			set int pnum 0 of {_packet} to (loop-value).getEntityId()
			send packet {_packet} to loop-value


# Name: Wake up - Wakes up player
# Example: wakeup player for player
effect wakeup %players% for %players%:
	trigger:
		set {_packet} to new play_server_entity_metadata packet
		set {_OptionalBlockPos} to Optional.empty()
		set {_dw} to new datawatcher
		set {_Serializer} to Registry.getBlockPositionSerializer(true) 
		set {_DWO} to new WrappedDataWatcherObject(13 and {_Serializer})
		{_dw}.setObject({_DWO} and {_OptionalBlockPos})		
		set {_Serializer2} to Registry.get(EnumWrappers.getEntityPoseClass())
		{_dw}.setObject(6, {_Serializer2}, EntityPose.STANDING.toNms())
		set watchable collection field 0 of {_packet} to {_dw}
		loop expressions-1:
			set int pnum 0 of {_packet} to (loop-value).getEntityId()
			send packet {_packet} to loop-value


# Name: Reqn content from URL - Gets content from URL using reqn
# Example: store content from URL "blob.com" to {_text}
effect store content from URL %string% to %objects%:
	trigger:
		delay the effect
		send a "get" request to "%expr-1%"
		set {_resp} to the last http response
		set {_body} to {_resp}'s body
		set raw expr-2 to {_body}
		continue


# Name: Pickup item - Fake pickups item for certain players
# Example: pickup item with id 100 by entity with id 101 for player
effect pickup item with id %number% by entity with id %number% amount %number% for %players%:
	trigger:
		set {_packet} to new PacketPlayOutCollect(expr-1, expr-2, expr-3)
		loop expressions-4:
			loop-value.getHandle().playerConnection.sendPacket({_packet})


# Name: Camera - Attaches player's camera to entity
# Example: attach camera of player to entity with id 101
effect attach camera of %players% to entity with id %number%:
	trigger:
		set {_packet} to new play_server_camera packet
		set int field 0 of {_packet} to expr-2
		send exprs-1 packet {_packet}


# Name: Passengers - Sets clientside passengers of entity
# Example: mount clientside entities with ids {_ids::*} count 2 to entity with id 101 for player
effect mount clientside entities with ids %integers% count %number% to entity with id %number% for %players%:
	trigger:
		set {_packet} to new play_server_mount packet
		set int field 0 of {_packet} to expr-3
		set int field 1 of {_packet} to expr-2
		set int array field 0 of {_packet} to exprs-1
		send exprs-4 packet {_packet}


#   ______________________________________________________________


#                       _
#                      | |      
#   _____   _____ _ __ | |_ ___ 
#  / _ \ \ / / _ \ '_ \| __/ __|
# |  __/\ V /  __/ | | | |_\__ \
#  \___| \_/ \___|_| |_|\__|___/


# Name: Clientside interact - Called when player interact with entity clientside (works for holograms and npcs too)
# Event values: player, clientside-event-entity, interaction-type
# Example: on clientside interact with entity: broadcast "%player% - %clientside-event-entity% - %interaction-type%"
event "clientsideEntityInteract":
	patterns: 
		clientside interact with entity
	event-values: player
expression clientside-event-entity:
	return type: integer
	usable in:
		custom event "clientsideEntityInteract"
	get:
		return event.getData("id")
expression interaction-type:
	return type: string
	usable in:
		custom event "clientsideEntityInteract"
	get:
		return event.getData("type")
on packet event play_client_use_entity:
	set {_values::player} to player
	set {_data::id} to (int field 0 of event-packet)
	set {_data::type} to (object field 1 of event-packet)
	call custom event "clientsideEntityInteract" with {_values::*} and data {_data::*}


# Name: Clientside digging - Called when player start/stop digging, finish action with item (eating, pulling bow...), drop item/stack, swap hands
# Event values: player, event-action-type, event-action-location, event-action-side
# Example: on clientside digging: broadcast "%player% - %event-action-type% - %event-action-location% - %event-action-side%"
event "clientsideDigging":
	patterns: 
		clientside digging
	event-values: player
expression event-action-type:
	return type: string
	usable in:
		custom event "clientsideDigging"
	get:
		return event.getData("type")
expression event-action-location:
	return type: location
	usable in:
		custom event "clientsideDigging"
	get:
		return event.getData("location")
expression event-action-side:
	return type: string
	usable in:
		custom event "clientsideDigging"
	get:
		return event.getData("side")
on packet event play_client_block_dig:
	set {_values::player} to player
	set {_data::type} to (object field 2 of event-packet)
	set {_data::location} to (location field 0 of event-packet)
	set {_data::side} to (object field 1 of event-packet)
	call custom event "clientsideDigging" with {_values::*} and data {_data::*}


# Name: Message recieve - Called when player recieves a chat message (You can cancel the event, very useful if you want to get rid of/replace some annoying plugin messages)
# Event values: player, recieved-message
# Example: on clientside message recieve: send "%player% - %recieved-message%" to console
event "clientsideMessageRecieve":
	patterns: 
		clientside message recieve
	event-values: player
expression recieved-message:
	return type: string
	usable in:
		custom event "clientsideMessageRecieve"
	get:
		return event.getData("message")
on packet event play_server_chat:
	set {_data::message} to "%(chat component field 0 of event-packet)%"
	set {_values::player} to player
	set {_event} to custom event "clientsideMessageRecieve" with {_values::*} and data {_data::*}
	call {_event}
	{_event} is cancelled
	cancel event


# Name: Advanced join - Called when player joins, can detect mods in client, version etc.
# Event values: player, vanilla, payload
# Example: on advanced join: broadcast "%player% - %vanilla% - %payload%"
event "advancedPayloadJoin":
	patterns: 
		advanced join
	event-values: player
expression vanilla:
	return type: boolean
	usable in:
		custom event "advancedPayloadJoin"
	get:
		return event.getData("vanilla")
expression payload:
	return type: string
	usable in:
		custom event "advancedPayloadJoin"
	get:
		return event.getData("payload")
on packet event play_client_custom_payload:
	set {_data::vanilla} to false
	set {_channel::*} to object field 0 of event-packet
	loop {_channel::*}:
		if "%loop-value%" is "MC|Brand" or "minecraft:brand":
			set {_message} to new String([byte buffer field of event-packet])
			{_message} contains "vanilla"
			set {_data::vanilla} to true
		set {_data::payload} to "%{_channel::*}%"
	set {_values::player} to player
	call custom event "advancedPayloadJoin" with {_values::*} and data {_data::*}


# Name: Resource pack status - Called when player successfully load/decline/failed download or accept resourcepack
# Event values: player, pack-status
# Example: on clientside resourcepack status: broadcast "%player% - %pack-status%"
event "clientsideResourcePackStatus":
	patterns: 
		clientside resourcepack status
	event-values: player
expression pack-status:
	return type: string
	usable in:
		custom event "clientsideResourcePackStatus"
	get:
		return event.getData("status")
on packet event play_client_resource_pack_status:
	set {_data::status} to "%(object field 0 of event-packet)%"
	set {_values::player} to player
	call custom event "clientsideResourcePackStatus" with {_values::*} and data {_data::*}


# Name: Sign gui update - Called when player change sign gui
# Event values: player, line-%integer%
# Example: on sign gui update: broadcast "%player% - %line-1%, %line-2%, %line-3%, %line-4%"
event "signUpdate":
    patterns: 
        sign gui update
    event-values: player
expression line-%number%:
	return type: string
	usable in:
		custom event "signUpdate"
	get:
		return event.getData("%expr-1%")
on packet event play_client_update_sign:
	set {_loc} to location field 0 of event-packet
	block at {_loc} is not a sign
	make player see block at {_loc} as block at {_loc}
	set {_values::player} to player
	loop string array field 0 of event-packet:
		add 1 to {_num}
		set {_data::%{_num}%} to loop-value
	call custom event "signUpdate" with {_values::*} and data {_data::*}


# Name: Block change - Called when block is changed (Can catch changing flower pots, activiting pistons etc.)
# Event values: player, block-location, block-data
# Example: on client-side block change: broadcast "%player% - %block-location% - %block-data%"
event "blockChange":
    patterns: 
        client-side block change
    event-values: player
expression block-location:
	return type: location
	usable in:
		custom event "blockChange"
	get:
		return event.getData("location")
expression block-data:
	return type: string
	usable in:
		custom event "blockChange"
	get:
		return event.getData("id")
packet event play_server_block_change:
	set {_values::player} to player
	set {_id} to object field 1 of event-packet
	set {_data::id} to "%{_id}%"
	set {_data::location} to location field 0 of event-packet
	call custom event "blockChange" with {_values::*} and data {_data::*}

# Name: Steer vehicle - Called when player steers vehicle
# Event values: player, sideways, foward, jump
# Example: on clientside steer: send "%player% - %sideways% - %foward% - %jump%" to player
event "clientsideSteer":
	patterns: 
		clientside steer
	event-values: player
expression sideways:
	return type: number
	usable in:
		custom event "clientsideSteer"
	get:
		return event.getData("sideways")
expression foward:
	return type: number
	usable in:
		custom event "clientsideSteer"
	get:
		return event.getData("foward")
expression jump:
	return type: boolean
	usable in:
		custom event "clientsideSteer"
	get:
		return event.getData("flags")
on packet event play_client_steer_vehicle:
	set {_data::sideways} to (float field 0 of event-packet)
	set {_data::foward} to (float field 1 of event-packet)
	set {_data::flags} to "%(object field 2 of event-packet)%" parsed as boolean
	set {_values::player} to player
	call custom event "clientsideSteer" with {_values::*} and data {_data::*}


#   ______________________________________________________________

command /papi [<text>]:
	aliases: /skpapi
	trigger:
		if player is not op:
			send formatted "&6&lSKPAPI: &6Server is running &bSkPapi-{@version}&6, get your own here: <link:https://github.com/Pesekjak/skPapi>&9&ngithub.com/Pesekjak/skPapi<reset>" to player
			stop trigger
		if arg-1 is "update":
			message "&6&lSKPAPI: &6Server is running &bSkPapi-{@version}&6..."
			message "&6Checking version..."
			send a "get" request to "https://raw.githubusercontent.com/Pesekjak/skPapi/main/version.txt"
			set {_resp} to the last http response
			set {_LastVer} to {_resp}'s body
			set {_LastVerChars::*} to {_LastVer} split by ""
			set {_LastVer} to ""
			loop {_LastVerChars::*}:
				if loop-value is "." or "1" or "2" or "3" or "4" or "5" or "6" or "7" or "8" or "9" or "0" or "-" or "B" or "E" or "T" or "A":
					set {_LastVer} to "%{_LastVer}%%loop-value%"
			if {_LastVer} is not "{@version}":
				send "&6&lSKPAPI: &6Detected version {@version}&6, the newest version is %{_LastVer}%" to player
				send formatted "&6Gew the newest version here: &9&n<link:https://github.com/Pesekjak/skPapi>github.com/Pesekjak/skPapi<reset>" to player
			else:
				message "&6&lSKPAPI: &6You running the latest version of SkPapi"
		else if arg-1 is "changelog":
			message "&6Checking changelog..."
			send a "get" request to "https://raw.githubusercontent.com/Pesekjak/skPapi/main/changelog.txt"
			set {_resp} to the last http response
			set {_ChangeLog} to {_resp}'s body
			send "" to player
			send "&6%{_ChangeLog}%" to player
		else if arg-1 is "about":
			message "&6&lSKPAPI: &6Server is running &bSkPapi-{@version}&6. Thanks for downloading!"
			message "&6Author: pesekjan"
			message "&6Credits &c<3&6: Mr.Darth, Govindas"
			send formatted "&6For better understanding syntaxes, check documentation here: <link:https://app.gitbook.com/@jakub-zahomolkou/s/skpapi/>&9&napp.gitbook.com/@jakub-zahomolkou/s/skpapi<reset>" to player
		else:
			message "&6&lSKPAPI: &6Server is running &bSkPapi-{@version}&6. Thanks for downloading!"
			loop "update", "changelog" and "about":
				message "&6- &7/papi %loop-value%"
